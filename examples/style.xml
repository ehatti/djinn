<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
  <xsl:template match="definition">
    <html>
      <!-- <head>
        <script type="text/x-mathjax-config">
          MathJax.Hub.Config({tex2jax: {inlineMath: [['$','$'], ['\\(','\\)']]}});
        </script>
        <script type="text/javascript"
          src="http://cdn.mathjax.org/mathjax/latest/MathJax.js?config=TeX-AMS-MML_HTMLorMML">
        </script>
      </head> -->
      <body>
        <h2>Definition: <xsl:value-of select="title"/></h2>
        <math displaystyle="true">
          <mi><xsl:value-of select="ident"/></mi>
          <mo>:=</mo>
          <xsl:apply-templates select="value"/>
        </math>
        <!-- <xsl:text>$$\mathtt{</xsl:text>
        <xsl:value-of select="ident"/>
        <xsl:text>}:=</xsl:text>
        <xsl:apply-templates select="value"/>
        <xsl:text>$$</xsl:text> -->
      </body>
    </html>
  </xsl:template>

  <xsl:template match="theorem">
    <html>
      <body>
        <h2>Theorem: <xsl:value-of select="title"/></h2>
        <math displaystyle="true">
          <xsl:apply-templates select="goal"/>
        </math>
        <h3>Proof.</h3>
        <xsl:apply-templates select="proof"/>
      </body>
    </html>
  </xsl:template>

  <xsl:template match="introduce">
    <div>
      <p>Introduce
        <xsl:for-each select="suppose/binding">
          <xsl:text> </xsl:text><xsl:value-of select="./@name"/>
        </xsl:for-each>
      and assume the following.</p>
      <ul>
        <xsl:for-each select="suppose/hypothesis">
          <li>
            <math displaystyle="true">
              <mi><xsl:value-of select="./@name"/></mi>
              <mo>:</mo>
              <xsl:apply-templates select="."/>
            </math>
          </li>
        </xsl:for-each>
      </ul>
      <p>
        The goal is now as follows.
      </p>
      <ul><li><math displaystyle="true">
        <xsl:apply-templates select="goal"/>
      </math></li></ul>
      <div style="padding-left: 40px">
        <xsl:apply-templates select="proof"/>
      </div>
    </div>
  </xsl:template>

  <xsl:template match="admit">
    <div>
      <p><strong>ADMIT</strong> this goal.</p>
    </div>
  </xsl:template>

  <xsl:template match="apply">
    <xsl:for-each select="cite">
      <p>
        Apply theorem 
        <a>
          <xsl:attribute name="href">
            <xsl:value-of select="."/><xsl:text>.html</xsl:text>
          </xsl:attribute>
          <xsl:value-of select="."/>
        </a>.
      </p>
    </xsl:for-each>

    <xsl:for-each select="assume">
      <p>Apply hypothesis <xsl:value-of select="./@name"/>.</p>
    </xsl:for-each>

    <ul>
      <xsl:for-each select="specialize">
        <li>
          <math displaystyle="true">
            <mi><xsl:value-of select="./@name"/></mi>
            <mo>:=</mo>
            <xsl:apply-templates select="."/>
          </math>
        </li>
      </xsl:for-each>
    </ul>
    <ul>
      <xsl:for-each select="justify">
        <li>
          <p><math displaystyle="true">
            <xsl:apply-templates select="goal"/>
          </math></p>
          <xsl:apply-templates select="proof"/>
        </li>
      </xsl:for-each>
    </ul>
  </xsl:template>

  <xsl:template match="function">
    <mo>(</mo>
    <mo>\</mo>
    <xsl:for-each select="bindings/binding/@name">
      <mi><xsl:value-of select="."/></mi>
    </xsl:for-each>
    <mo>.</mo>
    <xsl:apply-templates select="body"/>
    <mo>)</mo>
  </xsl:template>

  <xsl:template match="application">
    <xsl:apply-templates select="head"/>
    <mo>(</mo>
      <xsl:apply-templates select="argument[1]"/>
      <xsl:for-each select="argument[position()>1]">
        <mo>,</mo>
        <xsl:apply-templates select="."/>
      </xsl:for-each>
    <mo>)</mo>
  </xsl:template>

  <xsl:template match="reference">
    <mtext>
      <a>
        <xsl:attribute name="href">
          <xsl:value-of select="."/><xsl:text>.html</xsl:text>
        </xsl:attribute>
        <xsl:value-of select="."/>
      </a>
    </mtext>
  </xsl:template>

  <xsl:template match="variable">
    <!-- <xsl:text>\mathtt{</xsl:text> -->
    <mi><xsl:value-of select="./@name"/></mi>
    <!-- <xsl:text>}</xsl:text> -->
  </xsl:template>
</xsl:stylesheet>